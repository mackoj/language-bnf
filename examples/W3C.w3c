/* EBNF-like notation used in W3C specifications */

Grammar ::= (Rule (Space Comment)? Space? #xA)+

Space ::= (#x20 | #x9 | #xD | #xA)+

Comment ::= '/*' PrintedCharacter* '*/'

/* PrintedCharacter ::= the glyph which is directly displayed or printed out */

Rule ::= Symbol Space '::=' Space Expression

Symbol ::= Letter+   /* Symbols are written with an initial capital letter
                        if they are the start symbol of a regular language,
                        otherwise with an initial lowercase letter. */

Letter ::= [A-Za-z]

Expression ::= Concatenation (Space '|' Space Concatenation)* /* Alternation */

Concatenation ::= Repetition ((Space Repetition)* | (Space '-' Repetition)?)

Repetition ::= SimpleExpression [?+*]?  /* '?' means zero or one occurrence
                                           '+' means one or more occurrence
                                           '*' means zero or more occurrence */

SimpleExpression ::= EncodedCharacter
                   | PositiveCharacterSet
                   | NegativeCharacterSet
                   | SingleQuotedString
                   | DoubleQuotedString
                   | '(' Space? Expression Space? ')'

EncodedCharacter ::= '#x' HexaDigit+          /* code point in ISO/IEC 10646 */

HexaDigit ::= [0-9A-F]

PositiveCharacterSet ::= '[' (Enumeration | Range)+ ']'

NegativeCharacterSet ::= '[^' (Enumeration | Range)+ ']'

Enumeration ::= (EncodedCharacter | PrintedCharacter)+

Range ::= EncodedCharacter '-' EncodedCharacter | PrintedCharacter '-' PrintedCharacter

SingleQuotedString ::= "'" [^']* "'"

DoubleQuotedString ::= '"' [^"]* '"'

/* Other notations used in the W3C specifications are:
   [ wfc: ... ]
   well-formedness constraint; this identifies by name a constraint on well-formed documents associated with a production.
   [ vc: ... ]
   validity constraint; this identifies by name a constraint on valid documents associated with a production.
*/
